plugins {
  id 'java'
  id 'application'

  id 'com.google.cloud.tools.jib' version '3.4.0'
  id 'com.gorylenko.gradle-git-properties' version '2.4.1'
}

ext {
  shortName = 'replay-example-issue-198'
  groupName = 'replay.example'
  className = groupName + '.Application'

  replay_version = '2.3.1'
  replay_group = 'io.github.replay-framework'
}

application {
  mainClassName = project.ext.className
  applicationName = project.ext.shortName
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}

gitProperties {
    extProperty = 'git' // git properties will be put in a map at project.ext.git
    keys = ['git.commit.id', 'git.commit.id.abbrev', 'git.commit.time']
    customProperty 'git.commit.id.describe', { it.describe(always: true) }
}

jib {
  to.image = 'replay-example:issue-198'

  container {
    creationTime = project.provider { project.ext.git['git.commit.time'] }
  }
}

jar {
  manifest {
    attributes(
      'Build-Revision': project.provider { project.ext.git['git.commit.id.describe'] }
    )
  }
}

base {
  archivesName = project.ext.shortName
}

distributions {
  main {
    distributionBaseName = project.ext.shortName
  }
}

sourceSets {
  main {
    java {
      srcDir 'app'
    }
  }
}

processResources {
  from('app') { include 'play.plugins' }
}

compileJava {
  options.debugOptions.debugLevel = "source,lines,vars"
  options.fork = true
  options.compilerArgs << '-parameters' //<< '-Xlint:deprecation'
}

configurations.implementation {
  exclude group: 'org.hibernate'
  exclude group: 'org.hibernate.common'
  exclude group: 'net.bytebuddy'
}

repositories {
  mavenCentral()
}

dependencies {
  implementation("${replay_group}:javanet:${replay_version}")
  implementation("${replay_group}:framework:${replay_version}")
//  implementation("${replay_group}:fastergt:${replay_version}")
}
